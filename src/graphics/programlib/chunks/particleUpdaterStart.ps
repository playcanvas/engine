float saturate(float x) {
    return clamp(x, 0.0, 1.0);
}

vec3 unpack3NFloats(float src) {
    float r = fract(src);
    float g = fract(src * 256.0);
    float b = fract(src * 65536.0);
    return vec3(r, g, b);
}

vec4 tex1Dlod_lerp(sampler2D tex, vec2 tc, out vec3 w) {
    vec4 a = texture2D(tex, tc);
    vec4 b = texture2D(tex, tc + graphSampleSize);
    float c = fract(tc.x * graphNumSamples);

    vec3 unpackedA = unpack3NFloats(a.w);
    vec3 unpackedB = unpack3NFloats(b.w);
    w = mix(unpackedA, unpackedB, c);

    return mix(a, b, c);
}

void main(void)
{
    if (gl_FragCoord.x > numParticles) discard;

    vec4 tex = texture2D(particleTexIN, vec2(vUv0.x, 0.125));
    vec4 tex2 = texture2D(particleTexIN, vec2(vUv0.x, 0.375));
    vec4 texR = texture2D(particleTexIN, vec2(vUv0.x, 0.625));

    float rndFactor = texR.x;
    vec3 rndFactor3 = texR.xyz;

    float particleLifetime = lifetime;
    float life = tex2.w + delta;
    float particleRate = rate + rateDiv * rndFactor;

    if (life > 0.0) {
        float nlife = clamp(life / particleLifetime, 0.0, 1.0);
        vec3 localVelocityDiv;
        vec3 velocityDiv;
        vec3 paramDiv;
        vec3 localVelocity =    tex1Dlod_lerp(internalTex0, vec2(nlife, 0), localVelocityDiv).xyz;
        vec3 velocity =         tex1Dlod_lerp(internalTex1, vec2(nlife, 0), velocityDiv).xyz;

        localVelocity +=        (localVelocityDiv * vec3(2.0) - vec3(1.0)) * localVelocityDivMult * rndFactor3;
        velocity +=             (velocityDiv * vec3(2.0) - vec3(1.0)) * velocityDivMult * rndFactor3.zxy;

        addInitialVelocity(localVelocity, rndFactor3);

        if (gl_FragCoord.y < 1.0) {
            // Current particle state
            vec4 params =           tex1Dlod_lerp(internalTex2, vec2(nlife, 0), paramDiv);

            float rotSpeed = params.x;
            float rotSpeedDiv = paramDiv.y;

            rotSpeed +=             (rotSpeedDiv * 2.0 - 1.0) * rotSpeedDivMult * rndFactor3.y;

            tex.xyz += (emitterMatrix * localVelocity.xyz + velocity.xyz * emitterScale) * delta;
            tex.w += rotSpeed * delta;
        } else if (gl_FragCoord.y < 2.0) {
            // Velocity
            tex.xyz = (emitterMatrix * localVelocity.xyz + velocity.xyz * emitterScale);
        }
    }

    if (gl_FragCoord.y < 1.0) {
        if (life <= 0.0 || life > particleLifetime) {
            tex.xyz = calcSpawnPosition(rndFactor3, rndFactor);
            tex.w = mix(startAngle, startAngle2, rndFactor);
        }
    } else if (gl_FragCoord.y < 2.0) {
        if (life <= 0.0 || life > particleLifetime) {
            tex.xyz = vec3(0.0);
        }
        tex.w = life;
    }

    if (gl_FragCoord.y >= 2.0) tex = texR;
